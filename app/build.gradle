apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.0"


    defaultConfig {
        applicationId "me.msfjarvis.kpsconnect"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 250
        versionName "2.5.0-beta"
    }
    signingConfigs
    {
        release
        {
            // Obtain Password and Alias
            storeFile file("../${project.property('keystore.file')}")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
         release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false // Ignore translation stuff and annoying warnings
    }

}

dependencies {
    testCompile 'junit:junit:4.12'
    compile project(':material-intro')
    compile project(':apprate')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'org.jsoup:jsoup:1.7.2'
    compile('com.afollestad.material-dialogs:core:0.9.0.1@aar') {
        transitive = true
    }
    compile 'com.afollestad:bridge:3.2.5'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.mikepenz:aboutlibraries:5.7.0@aar') {
        transitive = true
    }
    compile 'me.pushy:sdk:1.0.10'
    compile 'com.baoyz.pullrefreshlayout:library:1.2.0'
    compile(
            [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.2.3'],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.2.3'],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.2.3']
    )
}

task askForPasswords <<
{
	def storePass
	def keyAlias
	def keyPass

	def keystorePropertiesFile = new File(project.property("keystore.properties"))

	if(project.hasProperty("keystore.properties") && keystorePropertiesFile.exists())
	{
		println "Loading keystore passwords from property file..."
		Properties properties = new Properties()
		properties.load(new FileInputStream(keystorePropertiesFile))
		storePass = properties['keystore.store.password']
		keyAlias  = properties['keystore.key.alias']
		keyPass  = properties['keystore.key.password']
	}
	else
	{
		println "Getting keystore passwords..."
		storePass = new String(System.console().readPassword("\nStore password: "))
		keyAlias  = new String(System.console().readLine("Key alias: "))
		keyPass  = new String(System.console().readPassword("Key password: "))
	}

	android.signingConfigs.release.storePassword = storePass
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPass
}


tasks.whenTaskAdded
{
	theTask ->
	if(theTask.name.equals("packageRelease"))
	{
		theTask.dependsOn "askForPasswords"
	}
}
